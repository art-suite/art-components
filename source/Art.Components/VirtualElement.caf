import &StandardImport, {} &Counters
{virtualElementPoolEnabled} = VirtualNode = &VirtualNode

devMode = &DevMode

class VirtualElement extends VirtualNode
  @abstractClass()

  @createVirtualElementFactories: (elementClassNames) ->
    &ArtObjectTreeFactory.createObjectTreeFactories
      merge
        &Helpers.objectTreeFactoryOptions
        class: @
      elementClassNames
      (elementClassName) ->
        (props, children) ->
          @_newVirtualElement
            elementClassName
            props
            children

  @_newVirtualElement: (elementClassName, props, children) ->
    new @ elementClassName, props, children

  #####################
  # CONSTRUCTION
  #####################
  constructor: (elementClassName, props, children) ->
    Counters.virtualElementCreated()
    super props
    @_initVirtualElement elementClassName, props, children

  init: (elementClassName, props, children) ->
    Counters.virtualElementReused()
    @_initVirtualNode props
    @_initVirtualElement elementClassName, props, children
    @

  emptyChildren = []
  _initVirtualElement: (elementClassName, props, children) ->
    @_elementClassName = elementClassName
    @_children = children ? emptyChildren
    @_validateChildren children if devMode
    @_element = null

  #####################
  # Getters
  #####################
  @getter "elementClassName released element",
    children: ->
      if !@_children
        throw new Error
          log.warn if @_released
                "Critical Failure: VirtualElement<#{@inspectedName}-#{@uniqueId}> was released! Should not be accessing children!"
          else  "Internal Failure: VirtualElement<#{@inspectedName}-#{@uniqueId}> has no children!"

      @_children

  #####################
  # Inspection
  #####################
  @getter
    inspectedName: ->
      {key, elementClassName} = @
      "VirtualElement-#{elementClassName}#{if key then "-" + key  else ''}"

    inspectedObjects: ->
      [@elementClassName]: merge {}
        key:      if @key? then @key
        props:    if objectHasKeys @props   then merge @props
        children: if @children?.length > 0  then toInspectedObjects @children

  findElements: (pattern, options, matches = []) ->
    if foundMatch = @testMatchesPattern pattern
      matches.push @

    if !foundMatch || options?.findAll
      each child in @children
        child.findElements pattern, options, matches
    matches

  eachInComponent: (f) ->
    super
    each child in @children
      child.eachInComponent f
    null

  #####################################
    Specialization Overrides
    (only used internally)
  #####################################
    Override these for custom ArtReact specializations (like ArtEngine)

    If your concrete-elements implement the following API you won't
    have much to do to create your own VirtaulElements:

      Element API:
        setProperty:    (key, value) ->
        resetProperty:  (key) ->
          # restore property to it's default

        setChildren:    (concreteChildrenArray) ->
          # set or Update (via complete replacement) element's children

    Then all you need to do is override:

      _newElement

    Optionally, you can override the following:

      _updateElementProps
      _setElementChildren
      withElement
      sendToElement

  ## _newElement
    OVERRIDE: required
    OUT: new concrete element instance
  _newElement: (elementClassName, props, childElements)->
    elementClassName

  _newErrorElement: -> @_newElement :ErrorElement

  ## _updateElementProps
    OVERRIDE: optional
    EFFECT: @props has been updated and any props on the Concrete Element have been update
    OUT: true if props changed
  elementTemp     = null
  addedOrChanged  = (k, v) -> elementTemp.setProperty k, v  unless k == :children
  removed         = (k, v) -> elementTemp.resetProperty k   unless k == :children
  _updateElementProps: (newProps) ->
    elementTemp = @element
    try
      out = @_updateElementPropsHelper newProps, addedOrChanged, removed
    elementTemp = null
    out

  ## _setElementChildren
    OVERRIDE: optional
    IN: childrenElement: array of concrete elements to be all the children for this element
  _setElementChildren: (childElements) ->
    @element.setChildren childElements

  #####################################
    Specialization Overrides
    (used Externally)
  #####################################

  ## withElement
    Evaluate "f" in the thread the Element exists in.

    Example: For when React is running in a web-worker and the element
    is on the main threa.

    EFFECT: execute the function 'f' with the Concrete-Element associated with this VirtualElement.
    IN: (element) -> plainObjects
    OUT: promise returning function's plain-object-result

    PURPOSE: This is provided for the web-worker React so you can access the concrete element even though it is
      in another context. In that case, "f" will be serialized and any closure will be lost...

    OVERRIDE this to support web-workers - when concrete elements are on not in this worker-instance
    OVERRIDE: optional
  withElement: (f) ->
    new Promise (resolve) -> resolve f @_element

  ## sendToElement
    Lighter-weight than "withElement"

    IN:
      method: string
      args: 0 or more additional arguments
    OUT: promise returning function's plain-object-result

    Equivelent to:
      @withElement (element) -> element[method] args...
    OVERRIDE this to support web-workers - when concrete elements are on not in this worker-instance
    OVERRIDE: optional
  sendToElement: (method, args...) ->
    new Promise (resolve) -> resolve @_element[method] args...

  ####################################
    Specialization Helpers
  ####################################
  ## _updateElementPropsHelper
    EFFECT: @props has been set to the newProps if they changed
    OUT: true if props changed
  _updateElementPropsHelper: (newProps, addedOrChanged, removed) ->
    oldPropsLength = @getPropsLength()
    oldProps = @props

    noChangeCount = 0
    noChange = -> noChangeCount++

    # objectDiff: (o1, o2, added, removed, changed, nochange, eq = defaultEq, o2KeyCount) ->
    newPropsLength = @setPropsLength objectDiff newProps, oldProps, addedOrChanged, removed, addedOrChanged, noChange, propsEq

    if newPropsLength == noChangeCount && oldPropsLength == newPropsLength
      false
    else
      @props = @_rawProps = newProps
      true

  ########################
  ########################
    PRIVATE
  ########################
  ########################

  ##########################################
    VirtualNode Overrides
  ##########################################
  clone: ->
    new VirtualElement
      @elementClassName
      @props
      array child in @children
        child.clone()

  _captureRefs: (component) ->
    if component == @renderedIn
      if (key = @key)?
        log.warn "Duplicate key #{key} found in #{@inspectedPath}" if component._refs[key]
        component._refs[key] = @

      each child in @children
        child._captureRefs component

    null

  _resetAllProps: ->
    Counters.virtualElementReleased()
    @_resetVirtualNodeProps()

    each child in-array @_children
      child.release()

    @_elementClassName = @_children = null

  _unmount: ->
    each child in @children
      child._unmount()

  #####################
  # Instantiate
  #####################

  ## _instantiate
    create element or componentInstance
    fully generate Virtual-AIM subbranch
    fully create all AIM elements
    OUT: this
  _instantiate: (parentComponent, parentVirtualNode) ->
    if parentComponent != @_renderedIn && parentComponent && @_renderedIn
      return @clone()._instantiate parentComponent, parentVirtualNode

    super
    Counters.virtualElementInstantiated()

    @_element = try
      instantiatedChildren = array child, childIndex in-array @children
        child = @children[childIndex] = child._instantiate parentComponent, @

        child.element
        ? throw new Error "child(#{child.uniqueId}) #{childIndex}/#{@children.length} #{child.inspectedName} not instantiated"

      @_element = @_newElement
        @elementClassName
        @props
        instantiatedChildren

    catch error
      errors = true
      log.error VirtualElement._instantiate: {}
          error
          @elementClassName
          @props

      @_newErrorElement()

    throw new Error "VirtualElement._instantiate: no @_element" unless @_element?

    @

  #################
    UpdateFrom
  #################
  ## _canUpdateFrom
    IN: <VirtualElement>
    OUT: <Boolean>
      True if this VE is capable of updating itself to identically match
      the passed-in virtualElement
  _canUpdateFrom: (virtualElement)->
    @elementClassName == virtualElement.elementClassName &&
    @key == virtualElement.key

  _extractOldChildToUpdate: (child)->
    find oldChild, i in @children
      if oldChild?._canUpdateFrom child
        @children[i] = null
        oldChild

  ## _fastUpdateChildren
    OUT: <Boolean>
      TRUE if no Nodes were added, removed or changed "types"
      This implies @element.children didn't change.

    EFFECT:
      if no Nodes were added, removed or changed "types"
        run _updateFrom newChild for all oldChildren
      else noop; return false
  _fastUpdateChildren: (fromVirtualElement) ->
    newChildren = fromVirtualElement.children
    oldChildren = @children
    return false unless oldChildren.length == newChildren.length

    each oldChild, i in-array oldChildren
      return false unless oldChild._canUpdateFrom newChildren[i]

    each oldChild, i in-array oldChildren
      oldChild._updateFrom newChildren[i]

    true

  validateIsVirtualElement: (ve) ->
    unless ve instanceof VirtualNode
      log {}
        self: @
        ve
      throw new Error "Expecting ve to be a virtualElement"
    ve

  ## _slowUpdateChildren
    IN: fromVirtualElement <VirtualElement>
  _slowUpdateChildren: (fromVirtualElement) ->

    oldChildren = @children

    newChildren = array newChild from-array fromVirtualElement.children
      if oldChild = @_extractOldChildToUpdate newChild
            oldChild._updateFrom newChild
      else  newChild._instantiate @_parentComponent, @

    each child in oldChildren when child
      child._unmount()
      child.release() if virtualElementPoolEnabled

    fromVirtualElement._children = null if virtualElementPoolEnabled

    @_children = newChildren
    @_updateElementChildrenFromVirtualChildren()

  _updateElementChildrenFromVirtualChildren: ->
    # log _updateElementChildrenFromVirtualChildren: before: @element.children[0]
    @_setElementChildren array child in-array @_children
      @validateIsVirtualElement child
      child.element
      ? throw new Error "VirtualElement: All children should be instantiated instances of VirtualNode."
    # log _updateElementChildrenFromVirtualChildren: after: @element.children[0]
    @


  ## _updateChildren
    OUT: <Boolean> true if children changed
      if true, element.setChildren was called
      if false, the children individually may change, but
        this element's children are the same set
  _updateChildren: (fromVirtualElement) ->
    if @_fastUpdateChildren fromVirtualElement
      false
    else
      @_slowUpdateChildren fromVirtualElement
      true

  # returns this
  _updateFrom: (fromVirtualElement) ->
    unless @_element                  then throw new Error "VirtualElement._updateFrom: #{@inspectedName} no @_element"
    if fromVirtualElement._released   then log.error "updating from released VirtualElement: #{fromVirtualElement.inspectedName}"
    if @released                      then log.error "updating at released VirtualElement: #{@inspectedName}"
    if fromVirtualElement == @        then throw new Error "_updateFrom self!!!"

    propsChanged    = @_updateElementProps fromVirtualElement.props
    childrenChanged = @_updateChildren fromVirtualElement

    @
